
WHITESPACE = _{ " " | "\t" | NEWLINE | ("#" ~ (!NEWLINE ~ ANY)*) | ("/*" ~ (!"*/" ~ ANY)* ~ "*/") }

CompUnit = { SOI ~ Item* ~ EOI }

Module = { "module" ~ Ident ~ "{" ~ Item* ~ "}" }

Item = { Use
       | Import
       | Def
       | Const
       | Type
       | Impl
       | Module
       }

Ident = { IdentNormal | IdentBuiltin | IdentUnderscore }

IdentNormal = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" )* }
IdentBuiltin = @{ "@" ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_" )* }
IdentUnderscore = @{ "_" ~ (ASCII_ALPHANUMERIC | "_" )* }

Import = { "import" ~ Ident }

Use = { "use" ~ UseTree }

UseTree = { Ident | UseTreeAll | UseTreeAlias | UseTreeMulti }

UseTreeAll = { Ident ~ "::" ~ "*" }
UseTreeAlias = { Ident ~ "as" ~ Ident }
UseTreeMulti = { Ident ~ "::" ~ "{" ~ UseTree ~ ("," ~ UseTree)* ~ "}" }
UseTreeSimple = { Ident ~ "::" ~ UseTree }

Const = { "const" ~ Ident ~ ":" ~ AccessExpr ~ "=" ~ Expr }

Type = { "type" ~ TypeSpec? ~ Ident ~ FnLhs? ~ ("=" ~ TypeBody)? }
TypeBody = { Struct | Interface | Expr | Constructors }
TypeSpec = { "builtin" }

Struct = { "struct" ~ "{" ~ (StructField ~ ("," ~ StructField)*)? ~ "}" }
StructField = { Ident ~ ":" ~ Expr }

Constructors = { "|" ~ Constructor ~ ("|" ~ Constructor)* }
Constructor = { Ident ~ FnTyParams? ~ (":" ~ AccessExpr)? }

Interface = { "interface" ~ "{" ~ Def* ~ "}" }

Impl = { "impl" ~ QualifyExpr ~ "{" ~ Def* ~ "}" }

Def = { "def" ~ DefSpec? ~ Ident ~ FnLhs ~ ("=>" ~ Expr)? }

DefSpec = { "builtin" }

Expr = {  Return | Break | Continue | FnExpr | Assign | OpExpr }

OpExpr = { PrefixOp* ~ AccessExpr ~ PostfixOp* ~ (InfixOp ~ PrefixOp* ~ AccessExpr ~ PostfixOp*)* }

FnExpr = { FnLhs ~ "=>" ~ Expr }
FnLhs = { FnParams ~ (":" ~ OpExpr)? }
FnParams = { "(" ~ (FnParam ~ ("," ~ FnParam)*)? ~ ","? ~ ")" }
FnParam = { ParamSpec? ~ Ident ~ (":" ~ OpExpr)? }

FnTy = { FnTyParams ~ "->" ~ OpExpr }
FnTyParams = { "(" ~ (FnTyParam ~ ("," ~ FnTyParam)*)? ~ ","? ~ ")" }
FnTyParam = { (ParamSpec? ~ Ident ~ ":")? ~ OpExpr }

ParamSpec = { "implicit" }

AccessExpr = { (PrimaryExpr ~ FnArgs* ~ ".")* ~ PrimaryExpr ~ FnArgs* }

PrimaryExpr = { Universe
              | Unit
              | Literal
              | ExprWithBlock
              | QualifyExpr
              | Ident
              | RangeFull
              | FnTy 
              | Tuple
              | "(" ~ Expr ~ ")"
              }

QualifyExpr = { (Ident ~ FnArgs* ~ "::")* ~ Ident }

FnArgs = { "(" ~ (FnArg ~ ("," ~ FnArg)*)? ~ ")" }
FnArg = { (Ident ~ "=")? ~ Expr }

Tuple = { "(" ~ Expr ~ ("," ~ Expr)+ ~ ")" }

ExprWithBlock = { Loop 
                | While 
                | For
                | If 
                | IfLet
                | Match
                | Block
                | StructInit
                }

StructInit = { QualifyExpr ~ "{" ~ (StructInitField ~ ("," ~ StructInitField)*)? ~ ","? ~ "}" }
StructInitField = { Ident ~ ":" ~ Expr }

Assign = { OpExpr ~ "=" ~ Expr }

Loop = { Label? ~ "loop" ~ Block }

Continue = { "continue" ~ Label? }

While = { Label? ~ "while" ~ Expr ~ Block }

For = { Label? ~ "for" ~ Pattern ~ "in" ~ Expr ~ Block }

If = { "if" ~ Expr ~ Block ~ ("else" ~ ExprWithBlock)?}

IfLet = { "if" ~ "let" ~ Pattern ~ "=" ~ Expr ~ Block ~ ("else" ~ ExprWithBlock)? }

RangeFull = { ".." }

Block = { BlockSpec? ~ Label? ~ "{" ~ Stmt* ~ "}" }
BlockSpec = { "comptime" }
Label = ${ "'" ~ Ident }

Stmt = { Let | Item | Expr }

Let = { "let" ~ PatternWithoutRange ~ (":" ~ AccessExpr)? ~ ("=" ~ Expr)? }

Match = { "match" ~ Expr ~ "{" ~ MatchArm ~ ("," ~ MatchArm)* ~ ","? ~ "}" }
MatchArm = { Pattern ~ "=>" ~ Expr }

Pattern = { "|"? ~ PrimaryPattern ~ ("|" ~ PrimaryPattern)* }

PrimaryPattern = { RangePattern | PatternWithoutRange }

PatternWithoutRange = { WildcardPattern
                      | RestPattern
                      | LiteralPattern
                      | StructPattern 
                      | ConstructorPattern
                      | QualifyPattern
                      | IdentPattern
                      | TuplePattern
                      | "(" ~ Pattern ~ ")"
                      }

LiteralPattern = { Literal }

IdentPattern = { IdentPatternSpec? ~ Ident }
IdentPatternSpec = { "comptime" 
                   | "mut" 
                   | "ref" 
                   | "ref" ~ "mut" 
                   }

WildcardPattern = { "_" }

RestPattern = { ".." }

StructPattern = { QualifyExpr ~ "{" ~ (StructPatternElem ~ ("," ~ StructPatternElem)*)? ~ ","? ~ "}" }
StructPatternElem = { StructPatternField | RestPattern }
StructPatternField = { Ident ~ ":" ~ Pattern }

RangePattern = { RangePatternInclusive 
               | RangePatternExclusive 
               | RangePatternFrom 
               | RangePatternTo 
               | RangePatternToInclusive
               }
RangePatternBound = { Literal | QualifyExpr }
RangePatternInclusive = { RangePatternBound ~ "..=" ~ RangePatternBound }
RangePatternExclusive = { RangePatternBound ~ ".." ~ RangePatternBound }
RangePatternFrom = { RangePatternBound ~ ".." }
RangePatternTo = { ".." ~ RangePatternBound }
RangePatternToInclusive = { "..=" ~ RangePatternBound }

ConstructorPattern = { QualifyExpr ~ ConstructorPatternArgs }
ConstructorPatternArgs = { "(" ~ (ConstructorPatternArg ~ ("," ~ ConstructorPatternArg)*)? ~ ")" }
ConstructorPatternArg = { (Ident ~ "=")? ~ Pattern }

TuplePattern = { "(" ~ Pattern ~ ("," ~ Pattern)+ ~ ")" } 

QualifyPattern = { (Ident ~ FnArgs* ~ "::")+ ~ Ident }

Return = { "return" ~ Expr? }

Break = { "break" ~ Label? ~ Expr? }

Universe = @{ "Type" }

Unit = @{ "Unit" }

Literal = { Floating | HexInteger | OctInteger | BinInteger | DecInteger }

LiteralSuffix = { "u8" 
                | "u16"
                | "u32"
                | "u64"
                | "i8"
                | "i16"
                | "i32"
                | "i64"
                | "f32"
                | "f64"
                | "usize"
                | "isize"
                }

HexPrefix = { "0x" | "0X" }
OctPrefix = { "0o" | "0O" }
BinPrefix = { "0b" | "0B" }

DecBody = { (ASCII_DIGIT | "_")+ }
HexBody = { (ASCII_HEX_DIGIT | "_")+ }
OctBody = { (ASCII_OCT_DIGIT | "_")+ }
BinBody = { (ASCII_BIN_DIGIT | "_")+ }

DecInteger = ${ DecBody ~ LiteralSuffix? }
HexInteger = ${ HexPrefix ~ HexBody ~ LiteralSuffix? }
OctInteger = ${ OctPrefix ~ OctBody ~ LiteralSuffix? }
BinInteger = ${ BinPrefix ~ BinBody ~ LiteralSuffix? }

Exponent = @{ ("e" | "E") ~ ("+" | "-")? ~ DecBody }
DecimalPart = @{ "." ~ DecBody }
Floating = ${ DecBody ~ DecimalPart ~ Exponent ~ LiteralSuffix? 
            | DecBody ~ Exponent ~ LiteralSuffix?
            | DecBody ~ DecimalPart ~ LiteralSuffix? 
            }

Add = { "+" }
Sub = { "-" }
Mul = { "*" }
Div = { "/" }
Rem = { "%" }
Shl = { "<<" }
Shr = { ">>" }

Range = { ".." }
RangeFrom = { ".." }
RangeTo = { ".." }
RangeInclusive = { "..=" }
RangeToInclusive  = { "..=" }

LogicalAnd = { "&&" }
LogicalOr  = { "||" }

BitwiseAnd = { "&" }
BitwiseOr  = { "|" }
BitwiseXor = { "^" }

Eq = { "==" }
Ne = { "!=" }
Lt = { "<" }
Le = { "<=" }
Gt = { ">" }
Ge = { ">=" }

Pos = { "+" }
Neg = { "-" }
Ref = { "&" }
Deref = { "*" }
LogicalNot = { "!" }
BitwiseNot = { "~" }

Path = { "===" }
IntervalMin = { "/\\" }
IntervalMax = { "\\/" }
IntervalNeg = { "~~" }

InfixOp = _{ Add 
           | Sub 
           | Mul 
           | IntervalMin
           | IntervalMax
           | Div 
           | Rem 
           | Shl
           | Shr
           | LogicalAnd 
           | LogicalOr 
           | BitwiseAnd 
           | BitwiseOr 
           | BitwiseXor 
           | Path
           | Eq 
           | Ne 
           | Lt 
           | Le 
           | Gt 
           | Ge
           | Range
           | RangeInclusive
           }

PrefixOp = _{ Pos 
            | Neg 
            | Ref 
            | Deref 
            | LogicalNot 
            | IntervalNeg
            | BitwiseNot 
            | RangeTo
            | RangeToInclusive
            }

PostfixOp = _{ RangeFrom }